# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

group("test") {
  testonly = true

  deps = [
    ":c_bindings_unit",
    ":c_system_unit",
    ":cpp_application_unit",
    ":cpp_bindings_perf",
    ":cpp_bindings_unit",
    ":cpp_environment_unit",
    ":cpp_system_unit",
    ":cpp_utility_unit",
  ]
}

source_set("test_support") {
  testonly = true

  sources = [
    "test_support.cc",
  ]

  deps = [
    "//mojo/public/cpp/test_support",
  ]
}

template("mojo_public_test") {
  assert(defined(invoker.deps), "Need deps in $target_name.")

  executable(target_name) {
    testonly = true
    output_name = "mojo_public_" + target_name + "tests"

    sources = [
      "run_all_unittests.cc",
    ]

    deps = [
             "//mojo/system",
             "//third_party/gtest",
           ] + invoker.deps
  }
}

mojo_public_test("c_bindings_unit") {
  deps = [
    "//mojo/public/c/bindings/tests",
  ]
}

mojo_public_test("c_system_unit") {
  deps = [
    "//mojo/public/c/system/tests",
  ]
}

mojo_public_test("cpp_application_unit") {
  deps = [
    "//mojo/public/cpp/application/tests",
  ]
}

mojo_public_test("cpp_environment_unit") {
  deps = [
    "//mojo/public/cpp/environment/tests",
  ]
}

mojo_public_test("cpp_system_unit") {
  deps = [
    "//mojo/public/cpp/system/tests",
  ]
}

mojo_public_test("cpp_utility_unit") {
  deps = [
    "//mojo/public/cpp/utility/tests",
  ]
}

mojo_public_test("cpp_bindings_perf") {
  deps = [
    ":test_support",
    "//mojo/public/cpp/bindings/tests:perftests",
  ]
}

mojo_public_test("cpp_bindings_unit") {
  deps = [
    ":test_support",
    "//mojo/public/cpp/bindings/tests",
  ]
}
