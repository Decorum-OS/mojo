# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

group("test") {
  testonly = true

  deps = [
    # Unit tests:
    ":mojo_public_c_bindings_unittests",
    ":mojo_public_c_system_unittests",
    ":mojo_public_cpp_application_unittests",
    ":mojo_public_cpp_bindings_unittests",
    ":mojo_public_cpp_environment_unittests",
    ":mojo_public_cpp_system_unittests",
    ":mojo_public_cpp_utility_unittests",

    # Perf tests:
    ":mojo_public_c_system_perftests",
    ":mojo_public_cpp_bindings_perftests",
    ":mojo_public_cpp_environment_perftests",
  ]
}

source_set("test_support") {
  testonly = true

  sources = [
    "test_support.cc",
  ]

  deps = [
    "//mojo/public/cpp/test_support",
  ]
}

template("mojo_public_test") {
  assert(defined(invoker.deps), "Need deps in $target_name.")

  executable(target_name) {
    testonly = true

    sources = [
      "run_all_unittests.cc",
    ]

    deps = [
             "//mojo/system",
             "//third_party/gtest",
           ] + invoker.deps
  }
}

# C unit tests:

mojo_public_test("mojo_public_c_bindings_unittests") {
  deps = [
    ":test_support",
    "//mojo/public/c/bindings/tests",
  ]
}

mojo_public_test("mojo_public_c_system_unittests") {
  deps = [
    "//mojo/public/c/system/tests",
  ]
}

# C++ unit tests:

mojo_public_test("mojo_public_cpp_application_unittests") {
  deps = [
    "//mojo/public/cpp/application/tests",
  ]
}

mojo_public_test("mojo_public_cpp_bindings_unittests") {
  deps = [
    ":test_support",
    "//mojo/public/cpp/bindings/tests",
  ]
}

mojo_public_test("mojo_public_cpp_environment_unittests") {
  deps = [
    "//mojo/public/cpp/environment/tests",
  ]
}

mojo_public_test("mojo_public_cpp_system_unittests") {
  deps = [
    "//mojo/public/cpp/system/tests",
  ]
}

mojo_public_test("mojo_public_cpp_utility_unittests") {
  deps = [
    "//mojo/public/cpp/utility/tests",
  ]
}

# C perf tests:

mojo_public_test("mojo_public_c_system_perftests") {
  deps = [
    ":test_support",
    "//mojo/public/c/system/tests:perftests",
  ]
}

# C++ perf tests:

mojo_public_test("mojo_public_cpp_bindings_perftests") {
  deps = [
    ":test_support",
    "//mojo/public/cpp/bindings/tests:perftests",
  ]
}

mojo_public_test("mojo_public_cpp_environment_perftests") {
  deps = [
    ":test_support",
    "//mojo/public/cpp/environment/tests:perftests",
  ]
}
